#!/usr/bin/perl -w

# $Id$

use strict;
use SVN::Notify;

# Parse out the options.
my $opts = SVN::Notify->get_options
  or require Pod::Usage && Pod::Usage::pod2usage(2);

# Handle version, help, man.
if (delete $opts->{version}) {
    print "svnnotify ", SVN::Notify->VERSION, $/;
    exit;
}
require Pod::Usage && Pod::Usage::pod2usage(1) if delete $opts->{help};
require Pod::Usage && Pod::Usage::pod2usage(-exitstatus => 0, -verbose => 2)
  if delete $opts->{man};

# Handle required options.
die qq{Missing required "--repos-path" parameter\n}
  unless $opts->{repos_path};
die qq{Missing required "--revision" parameter\n}
  unless $opts->{revision};
die qq{Missing required "--to" or "--to_regex_map" parameter\n}
  unless $opts->{to} || $opts->{to_regex_map};

# What class are we using?
my $class = 'SVN::Notify';
$class .= '::' . delete $opts->{handler} if $opts->{handler};

# Do it!
my $notifier = $class->new(%$opts);
$notifier->prepare;
$notifier->execute;

1;
__END__

=head1 Name

svnnotify - Subversion activity notification

=head1 Synopsis

=begin comment

Fake-out Pod::Usage

=head1 SYNOPSIS

=end comment

  svnnotify --repos-path "$1" --revision "$2" [options]

=head1 Options

  -p --repos-path PATH         Path to the Subversion repository. Required.
  -r --revision REVISION       Commit revision number. Required.
  -t --to ADDRESS              The notification destination email address.
                               Required unless --to-regex-map.
  -x --to-regex-map TO=REGEX   A mapping between a destination email address
                               and a regular expresion to match against the
                               directories affected by the commit. Required
                               unless --to.
  -f --from ADDRESS            Email address to use in the From header.
  -D --user-domain DOMAIN      Domain name to append to the username to
                               complete the email address in the From header.
  -l --svnlook SVNLOOK         Location of the svnlook executable. Defaults
                               to "/usr/local/bin/svnlook".
  -s --sendmail SENDMAIL       Location of the sendmail executable. Defaults
                               to "/usr/sbin/sendmail".
  -c --charset CHARSET         The character set of the log message and the
                               repository files.
  -d --with-diff               Include the diff in the message.
  -a --attach-diff             Attach the diff to the message.
  -R --reply-to ADDRESS        Address for use in the Reply-To header.
  -P --subject-prefix PREFIX   String to prepend to the subject.
  -C --subject-cx              Include the context of the commit in the
                               subject.
  -X --strip-cx-regex          Regex to remove part of the CX file name from
                               the subject.
  -O --no-first-line           Do not include the first line of the log
                               message in the subject.
  -i --max-sub-length LENGTH   Maximum size of the subject line.
  -H --handler HANDLER         The notification handler, such as "HTML".
  -U --viewcvs-url URL         Include links to specified ViewCVS URL.
  -T --rt-url URL              Include links to specified Request Tracker URL.
  -B --bugzilla-url URL        Include links to specified Bugzilla URL.
  -J --jira-url URL            Include links to specified JIRA URL.
  -V --verbose                 Incremental verbose mode.
  -h --help                    Print this usage statement and exit.
  -m --man                     Print the complete documentation and exit.
  -v --version                 Print the version number and exit.

More options may be supported by the subclass of SVN::Notify specified by the
C<--handler> option. Consult the documentation of the relevant subclass for
details.

=head1 See Also

See L<SVN::Notify|SVN::Notify> for the complete documentation.

=head1 Author

David Wheeler <david@kineticode.com>

=head1 Copyright and License

Copyright (c) 2004 Kineticode, Inc. All Rights Reserved.

This module is free software; you can redistribute it and/or modify it under the
same terms as Perl itself.

=cut
